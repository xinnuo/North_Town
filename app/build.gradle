apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply from: '../dependency.gradle'

repositories {
    mavenCentral()
    flatDir { dir 'libs' }
}

android {
    compileSdkVersion versions.compileSdkVersion
    defaultConfig {
        applicationId "com.ruanmeng.north_town"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode 101
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        applicationVariants.all { variant ->
            if (variant.buildType.name == "release") {
                variant.outputs.all { output ->
                    def file = output.outputFile
                    if (file != null && file.name.endsWith('.apk')) {
                        // def version = "v${versionName}(${versionCode})"
                        // def formatDate = new Date().format("yyyyMMddHHmm", TimeZone.getTimeZone("UTC"))
                        // def fileName = file.name.replace(".apk", "-${version}-${formatDate}.apk")
                        // def fileName = file.name.replace(".apk", "-v${versionName}.apk")
                        def fileName = "${applicationId}-v${versionName}.apk"
                        outputFileName = fileName
                    }
                }
            }
        }

        //支持 armeabi-v7a 和 x86 架构 CPU
        //处理.so文件时的重要法则:要么全部支持，要么都不支持
        ndk { abiFilters "armeabi-v7a", "x86" }
    }
    buildTypes {
        debug {
            //显示Log打印输出
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "String", "API_HOST", "\"http://122.114.212.120:8082\""
            minifyEnabled false   //混淆编译
            zipAlignEnabled false //Zip代码压缩优化
        }
        release {
            //不显示Log打印输出
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "String", "API_HOST", "\"http://122.114.212.120:8082\""
            minifyEnabled false  //混淆编译
            zipAlignEnabled true //Zip代码压缩优化
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        debug {
            storeFile file('../North_Town.jks')
            storePassword "123456"
            keyAlias "North_Town"
            keyPassword "123456"
        }
    }

    //移除lint检查的error（程序在buid的时候，会执行lint检查）
    lintOptions {
        abortOnError false  // true--错误发生后停止gradle构建
        ignoreWarnings true // true--只报告error
        showAll true        // true--显示错误的所有发生位置，不截取
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main { jniLibs.srcDirs = ['libs'] }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.android.support:appcompat-v7:' + versions.supportLibraryVersion
    implementation 'com.android.support:recyclerview-v7:' + versions.supportLibraryVersion
    implementation 'com.android.support:design:' + versions.supportLibraryVersion
    implementation 'com.android.support:support-v4:' + versions.supportLibraryVersion
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:' + versions.kotlin_version
    implementation 'com.android.support.constraint:constraint-layout:' + versions.constraintLayoutVersion
    implementation 'com.android.support:multidex:' + versions.multidexVersion
    testImplementation 'junit:junit:4.12'

    implementation project(':okgo')
    implementation(name: 'BouncingJelly-1.0.4', ext: 'aar')
    implementation(name: 'FlycoDialog-1.3.2', ext: 'aar')
    implementation(name: 'MNProgressHUD-1.0.1', ext: 'aar')
    implementation(name: 'SuperText-1.1.1', ext: 'aar')
    implementation(name: 'RoundedImageView-2.3.0', ext: 'aar')

    implementation 'com.github.bumptech.glide:glide:' + versions.glideVersion
    kapt 'com.github.bumptech.glide:compiler:' + versions.glideVersion

    implementation('com.github.LuckSiege.PictureSelector:picture_library:v2.2.0') {
        exclude group: 'com.github.bumptech.glide', module: 'glide'
    }

    implementation('com.qianwen:update-app-kotlin:1.1.0') {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jre7'
    }

    implementation 'com.yanzhenjie:permission:1.1.2'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'net.idik:slimadapter:2.1.2'
    implementation 'com.fang:dashview:v1.0.0'
    implementation 'com.github.santalu:mask-edittext:1.0.1'
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'
    implementation 'com.weigan:loopView:0.1.2'
}
